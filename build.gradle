plugins {
    id 'java'
    id 'application'
    id 'com.vaadin' version '0.8.0'
}

defaultTasks("clean", "build")

def jettyVersion = "9.4.31.v20200723"

repositories {
    jcenter()
}

dependencies {
    // Vaadin 14
    implementation("com.vaadin:vaadin-core:14.3.2") {
//         Webjars are only needed when running in Vaadin 13 compatibility mode
        ["com.vaadin.webjar", "org.webjars.bowergithub.insites",
         "org.webjars.bowergithub.polymer", "org.webjars.bowergithub.polymerelements",
         "org.webjars.bowergithub.vaadin", "org.webjars.bowergithub.webcomponents"]
                .forEach { group -> exclude(group: group) }
    }

    implementation("javax.servlet:javax.servlet-api:3.1.0")
    implementation("org.eclipse.jetty:jetty-continuation:${jettyVersion}")
    implementation("org.eclipse.jetty:jetty-server:${jettyVersion}")
    implementation("org.eclipse.jetty.websocket:websocket-server:${jettyVersion}")
    implementation("org.eclipse.jetty.websocket:javax-websocket-server-impl:${jettyVersion}") {
        exclude(module: "javax.websocket-client-api")
    }

    // logging
    // currently we are logging through the SLF4J API to SLF4J-Simple. See src/main/resources/simplelogger.properties file for the logger configuration
    implementation("org.slf4j:slf4j-simple:1.7.30")

    // testing
    testCompile("com.github.mvysny.kaributesting:karibu-testing-v10:1.1.29")
    testCompile("org.junit.jupiter:junit-jupiter-engine:5.6.2")
}

test {
    useJUnitPlatform()
    testLogging {
        exceptionFormat = "FULL"
    }
}

def zip = task zip(type: Zip) {
    from("src/main/scripts")
    into("libs") {
        from(configurations.runtimeClasspath.allArtifacts.files)
        from(configurations.runtimeClasspath)
    }
}

artifacts {
    archives(zip)
}

application {
    mainClassName = "com.vaadin.starter.skeleton.ManualJetty"
}

vaadin {
    pnpmEnable = true
}
